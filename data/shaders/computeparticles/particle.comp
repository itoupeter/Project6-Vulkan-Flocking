#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

struct Particle
{
	vec2 pos;
	vec2 vel;
};

// LOOK: These bindings correspond to the DescriptorSetLayouts and
// the DescriptorSets from prepareCompute()!

// Binding 0 : Particle storage buffer (read)
layout(std140, binding = 0) buffer ParticlesA
{
   Particle particlesA[ ];
};

// Binding 1 : Particle storage buffer (write)
layout(std140, binding = 1) buffer ParticlesB
{
   Particle particlesB[ ];
};

layout (local_size_x = 256, local_size_y = 1) in;

// LOOK: rule weights and distances, as well as particle count, based off uniforms.
// The deltaT here has to be updated every frame to account for changes in
// frame rate.
layout (binding = 2) uniform UBO
{
	float deltaT;
	float rule1Distance;
	float rule2Distance;
	float rule3Distance;
	float rule1Scale;
	float rule2Scale;
	float rule3Scale;
	int particleCount;
} ubo;

void main()
{
	// LOOK: This is very similar to a CUDA kernel.
	// Right now, the compute shader only advects the particles with their
	// velocity and handles wrap-around.
	// TODO: implement flocking behavior.

    // Current SSBO index
    uint index = gl_GlobalInvocationID.x;
	// Don't try to write beyond particle count
    if (index >= ubo.particleCount)
		return;

    // Read position and velocity
	vec2 vPos = particlesA[index].pos;
    vec2 vVel = particlesA[index].vel;

	// flocking calculation
	{
		vec2 centerOfMass = vec2(0.);
		vec2 averageVelocity = vec2(0.);
		int neighbors1 = 0;
		int neighbors3 = 0;

		for (int i = 0; i < ubo.particleCount; ++i) {
			// skip self
			if (i == index) continue;

			float dist = distance(particlesA[i].pos, vPos);

			// cohesion
			if (dist < ubo.rule1Distance) {
				centerOfMass += particlesA[i].pos;
				neighbors1 += 1;
			}

			// seperation
			if (dist < ubo.rule2Distance) {
				vVel += (vPos - particlesA[i].pos) * ubo.rule2Scale;
			}

			// velocity adaptation
			if (dist < ubo.rule3Distance) {
				averageVelocity += particlesA[i].vel;
				neighbors3 += 1;
			}
		}

		if (neighbors1 > 0) {
			vVel += (centerOfMass / neighbors1 - vPos) * ubo.rule1Scale;
		}
		if (neighbors3 > 0) {
			vVel += averageVelocity / neighbors3 * ubo.rule3Scale;;
		}
	}

	// clamp velocity for a more pleasing simulation.
	vVel = normalize(vVel) * clamp(length(vVel), 0.0, 0.1);

	// kinematic update
	vPos += vVel * ubo.deltaT;

    // Wrap around boundary
	if (vPos.x < -1.0) vPos.x = 1.0;
	if (vPos.x > 1.0) vPos.x = -1.0;
	if (vPos.y < -1.0) vPos.y = 1.0;
	if (vPos.y > 1.0) vPos.y = -1.0;

	// Write back
    particlesB[index].pos = vPos;
    particlesB[index].vel = vVel;
}
